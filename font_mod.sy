use game

glyph_id_num :: fn n: int -> (int, int) {
    if n < 0 || n > 9 {
        print "number not in range:"
        print n
        ret (13, 0)
    }
    ret (n, 0)
}

glyph_id_const :: fn c: int -> (int, int) {
    if c < 0 || c > 1 {
        print "constant not in range:"
        print c
        ret (13, 0)
    }
    ret (10 + c, 0)
}

draw_num :: fn x: float, y: float, scale: float, prefix: int, number: int, suffix: int -> void {
    draw_num_tint! x, y, scale, prefix, number, suffix, (1.0, 1.0, 1.0, 1.0)
}

draw_num_tint :: fn x: float, y: float, scale: float, prefix: int, number: int, suffix: int, tint: (float, float, float, float) -> void {
    x := x
    number := clamp(number, 0, 9999)
    kern_x := scale / 1.3

    if prefix >= 0 {
        l_gfx_sprite! game.font(), glyph_id_const(prefix), (x, y), (scale, scale), tint
        x += kern_x
    }

    drawing := false
    draw_part :: fn part: int {
        if !drawing && part == 0 {
            ret
        }
        drawing = true
        l_gfx_sprite! game.font(), glyph_id_num(part), (x, y), (scale, scale), tint
        x += kern_x
    }
    draw_part! rem(number / 1000, 10)
    draw_part! rem(number / 100, 10)
    draw_part! rem(number / 10, 10)
    draw_part! rem(number / 1, 10)

    if number == 0 {
        l_gfx_sprite! game.font(), glyph_id_num(number), (x, y), (scale, scale), tint
        y += kern_x
    }

    if suffix >= 0 {
        l_gfx_sprite! game.font(), glyph_id_const(suffix), (x, y), (scale, scale), tint
    }
}
